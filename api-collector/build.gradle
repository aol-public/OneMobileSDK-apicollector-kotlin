/*
 * Copyright (c) 2018. Oath.
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy of this software and
 * associated documentation files (the "Software"), to deal in the Software without restriction,
 * including without limitation the rights to use, copy, modify, merge, publish, distribute, sublicense,
 * and/or sell copies of the Software, and to permit persons to whom the Software is furnished to do
 * so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all copies or substantial
 * portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED,
 * INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A
 * PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR
 * COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN
 * AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION
 * WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 */

ext {
    // Maven
    GROUP_ID = 'com.aol.one.publishers.android'
    ARTIFACT_ID = 'api-collector'
    LIBRARY_VERSION = GIT_BASED_VERSION
    IS_ANDROID = false

    def annotationsDir = project(':annotations').projectDir
    def annotationsHeadTag = "git tag --contains HEAD".execute(null, annotationsDir).text.trim()
    def annotationsGitVersion = "git describe --tags --abbrev=0".execute(null, annotationsDir).text.trim() ?: "1.0"

    ANNOTATIONS_VERSION = annotationsHeadTag ?: "$annotationsGitVersion-SNAPSHOT"
}

apply from: 'https://raw.githubusercontent.com/aol-public/OneMobileSDK-tools-android/master/github-maven.gradle'
apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'kotlin-kapt'

kapt {
    arguments {
        arg("BUILD_PATH_KEY", "$buildDir.absolutePath")
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$KOTLIN_VERSION"
    implementation "com.github.salomonbrys.kotson:kotson:2.5.0"

    kaptTest project(':api-collector')
    testCompileOnly project(":annotations")

    testImplementation project(':api-collector')
    testImplementation "com.github.salomonbrys.kotson:kotson:2.5.0"
    testImplementation "org.assertj:assertj-core:3.9.0"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$KOTLIN_VERSION"

    if (CI) {
        implementation "com.aol.one.publishers.android:annotations:$ANNOTATIONS_VERSION"
    } else {
        implementation project(":annotations")
    }
}