apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'maven'
apply plugin: 'org.ajoberstar.git-publish'

group = 'com.aol.one.publishers.android'
project.archivesBaseName = 'api-collector'
version = GIT_BASED_VERSION

buildscript {
    ext {
        kotlinVersion = '1.1.51'

        CI = System.getenv('CI')
        CI_TAG = System.getenv('TRAVIS_TAG')
        GIT_SHA = "git rev-parse --short HEAD".execute(null, projectDir).text.trim()
        GIT_BRANCH = "git rev-parse --abbrev-ref HEAD".execute(null, projectDir).text.trim()
        GIT_TAG = "git describe --tags --abbrev=0".execute(null, projectDir).text.trim().split("\\.")
        GIT_TAG = GIT_TAG.length > 1 ? GIT_TAG : ["0", "0"]
        GIT_BASED_VERSION = CI_TAG ? CI_TAG : GIT_TAG[0] + '.' + (GIT_TAG[1].toInteger() + 1)
    }

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.ajoberstar:gradle-git-publish:0.3.0"
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    compile 'com.github.salomonbrys.kotson:kotson:2.5.0'
}

gitPublish {
    repoUri = 'https://github.com/aol-public/OneMobileSDK-releases-android.git'
    branch = 'maven'
    preserve {
        include '**/*'
    }
    commitMessage "Version ${version}"
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: gitPublish.repoDir.toURI())
        }
    }

    dependsOn gitPublishReset
}

gitPublishCommit.dependsOn uploadArchives

